scriptname _Camp_ExaminePowerController extends ObjectReference
{Attached to object generated by power explosion.}

import CommonArrayHelper
import CampUtil
import _CampInternal

ObjectReference ReplacedRef
ObjectReference NewRef
ObjectReference[] ExtraDisabledRefs


Event OnInit()
	CampDebug(1, "Examine Controller " + self + " initialized.")
	_Camp_Compatibility c = GetCompatibilitySystem()
	int i = 0
	int len = ArrayCountForm(c.ExamineFormsToReplace)
	bool replaced = false
	while i < len
		replaced = SearchAndReplaceForm(c, i)
		if replaced
			return
		endif
		i += 1
	endWhile

	if !replaced
		debug.notification("You don't see anything worth investigating.")
		;debug.notification("Nothing in particular stands out.")
		;debug.notification("You see nothing noteworthy.")
		;debug.notification("You don't see anything useful.")
		self.Disable()
		self.Delete()
	endif
EndEvent

bool function SearchAndReplaceForm(_Camp_Compatibility akCompatibility, int iIndex)
	ObjectReference ref = Game.FindClosestReferenceOfTypeFromRef(akCompatibility.ExamineFormsToReplace[iIndex], self, 256.0)
	if ref && ref.IsEnabled()
		; Replace Object
		akCompatibility.ExamineSuccessMessages[iIndex].Show()
		ReplacedRef = ref
		NewRef = ReplacedRef.PlaceAtMe(akCompatibility.ExamineReplacementForms[iIndex], abInitiallyDisabled = true)
		NewRef.SetScale(ReplacedRef.GetScale())
		
		; Set Ownership
		Cell current_cell = ReplacedRef.GetParentCell()
		Faction fowner = current_cell.GetFactionOwner()
		if fowner
			NewRef.SetFactionOwner(fowner)
		else
			ActorBase aowner = current_cell.GetActorOwner()
			if aowner
				NewRef.SetActorOwner(aowner)
			endif
		endif

		ReplacedRef.DisableNoWait()
		NewRef.Enable()
		; shader effect on new thing
		if akCompatibility.ExamineExtraFormsToDisable[iIndex]
			DisableExtraObjects(akCompatibility.ExamineExtraFormsToDisable[iIndex])
		endif

		CampDebug(1, "Examine Controller " + self + " replaced " + ReplacedRef + " with " + NewRef + ", registering for update in 72 hours.")
		RegisterForSingleUpdateGameTime(1.0)
		return true
	else
		return false
	endif
endFunction

function DisableExtraObjects(FormList akExtraFormsToDisable)
	ExtraDisabledRefs = new ObjectReference[32]
	int list_size = akExtraFormsToDisable.GetSize()
	int i = 0
	while i < list_size
		ObjectReference ref = Game.FindClosestReferenceOfTypeFromRef(akExtraFormsToDisable.GetAt(i), self, 512.0)
		if ref
			ArrayAddRef(ExtraDisabledRefs, ref)
			ref.Disable()
		endif
		i += 1
	endWhile
endFunction

function EnableExtraObjects()
	if ExtraDisabledRefs
		int len = ArrayCountRef(ExtraDisabledRefs)
		if len > 0
			int i = 0
			while i > len
				ExtraDisabledRefs[i].EnableNoWait()
				i += 1
			endWhile
		endif
	endif
endFunction

Event OnUpdateGameTime()
	if NewRef
		if NewRef as _Camp_PlaceableObjectBase
			(NewRef as _Camp_PlaceableObjectBase).TakeDown()
		else
			NewRef.DisableNoWait()
			NewRef.Delete()
		endif
	endif
	if ReplacedRef
		ReplacedRef.Enable()
	endif
	EnableExtraObjects()

	ReplacedRef = None
	NewRef = None
	ExtraDisabledRefs = new ObjectReference[32]
	CampDebug(1, "Examine Controller " + self + " expired. Goodbye!")
	self.Disable()
	self.Delete()
EndEvent

Event Campfire_GameReloaded()

endEvent